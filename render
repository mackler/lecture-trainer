#!/usr/bin/perl

use strict;
use warnings;
use IPC::Open2;
use IPC::Open3;
use File::Temp qw/ tempfile /;
use POSIX qw(ceil);

unless ($ARGV[0]) {
    print STDERR "usage: $0 <textfile-name>\n";
    exit 1;
}

print "Reading from " . $ARGV[0] . "\n";

local $/ = undef;

my $file = $ARGV[0];
my $text = do {
    open my $fh, '<:encoding(UTF-8)', $file
        or die "could not open $file: $!";
    <$fh>;
};

my $debug = 1;
my $outdir = $file;
$outdir =~ s,^(.*/)?([^/]+)\.txt$,audio/$2,;
unless (-d "audio") { mkdir "audio" or die "unable to make audio folder: $!"; }
unless (-d $outdir) {
  mkdir $outdir or die "unable to make $outdir: $!";
}
print "Writing audio files to $outdir\n";

my @pieces = split("\n{2,}", $text);

my $count = scalar(@pieces);
# How many leading zeroes needed in the filenames?
my $count_places = length($count);
open(BACKWARD, ">", $outdir."/backward.m3u");
open(FORWARD, ">", $outdir."/forward.m3u");
#for (my $i = 0; $i < $count; $i++) {
for (my $i = 0; $i < $count; $i++) {
    my $outfile = sprintf("%0".$count_places."d.mp3", $i);
    print("$i. ");
    my($index) = $count - 1 - $i;
    $pieces[$index] =~ s/[\n\r]+/ /sg;
    if ($debug) { print($pieces[$index] . "\n"); }
    open2(\*ESPEAK_OUT, \*ESPEAK_IN, 'espeak-ng --stdout') or die "open2() failed $!";
    print ESPEAK_IN $pieces[$index];
    close(ESPEAK_IN);
    my $sound = <ESPEAK_OUT>;
    close(ESPEAK_OUT);
    if($?) { die "Failed to synthesize \"" . $pieces[$index] . "\"\n"; }

    open3(\*LENGTH_IN, \*LENGTH_OUT, \*LENGTH_ERR, 'sox - -n stat ') or die "open3() failed $!";
    print LENGTH_IN $sound;
    close(LENGTH_IN);
    my $seconds = <LENGTH_ERR>;
    $seconds =~ s/.*Length \(seconds\): *([0-9.]+).*/$1/s;
    close(LENGTH_OUT);
    close(LENGTH_ERR);

    my($fh, $tmpfile) = tempfile("XXXXXXXX", SUFFIX => '.wav', UNLINK => 1);
    open(PAD, "|-", "sox", "-", $tmpfile, "pad", "0", $seconds) or die "open() failed $!";
    print PAD $sound;
    close(PAD);
    close($fh);

    system("lame", "--quiet", $tmpfile, $outdir."/".$outfile);

    print BACKWARD "$outfile\n";
    print FORWARD sprintf("%0".$count_places."d", $index) . ".mp3\n";
}
print("\n");

close(BACKWARD);
close(FORWARD);
